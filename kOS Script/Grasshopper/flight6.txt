Copy AltitudeToVelocityPID from 0.
Copy VelocityToThrustPID from 0.
Lock throttle to 0.
Lock steering to UP + r(0,0,180).
Set padheight to alt:radar.
Lock alt to alt:radar - padheight.
Set maxheight to alt.
Set KpAV to 0.200.
Set KiAV to 0.000.
Set KdAV to 0.050.
Set integralAV to 0.
Set outMaxAV to 15.
Set outMinAV to -15.
Set KpVT to 0.100.
Set KiVT to 0.200.
Set KdVT to 0.005.
Set integralVT to 0.
Set outMaxVT to 1.
Set outMinVT to 0.1.
Stage.
Print 3.
Wait 1.
Print 2.
Wait 1.
Print 1.
Wait 1.
Print "Blastoff!".
Set starttime to missiontime.
Lock stagetime to missiontime - starttime.
Set dt to 0.05.
Set desiredAltitude to 325.
Set targetAltitude to 0.
Set ascend to 1.
Set previousA to 0.
Set previousV to 0.
Until stagetime > 10 and alt < 0.5 {
	If ascend = 1 and targetAltitude < desiredAltitude {
		Set targetAltitude to min(desiredAltitude, targetAltitude + 10).
	}.
	If ascend = 0 and targetAltitude > desiredAltitude {
		Set targetAltitude to max(desiredAltitude, targetAltitude - 10).
	}.
	Run AltitudeToVelocityPID(targetAltitude).
	Set desiredVelocity to outAV.
	Run VelocityToThrustPID(desiredVelocity).
	Lock throttle to outVT.
	Set previousA to alt.
	Set previousV to verticalspeed.
	Wait dt.
	If alt >= 325 {
		Set desiredAltitude to -5.
		Set ascend to 0.
	}.
	If alt > maxheight{
		Set maxheight to alt.
	}.
}.
Print "Duration: " + stagetime.
Print "Altitude: " + maxheight.